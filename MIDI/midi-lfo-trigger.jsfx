desc:Picker MIDI LFO Trigger
About:
  # About
  *MIDI LFO Trigger* is a JSFX plugin that creates a linear LFO every time a MIDI note is sent. The LFO is sent to the hidden "Output" slider.
Link: https://github.com/marcopicker/JSFX
Version: 0.2
Author: Marco Picker
Changelog:
	Updated the name and GitHub link.

slider1:channel=0<0,17,1{All,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>MIDI Channel

slider3:timebase=0<0,1,1{Real Time, Tempo}>Time Base
slider4:length_ms=100<0,10000,1:sqr=2>Length (ms)
slider5:length_beats=0.125<0,4,0.125>Length (beats)

slider6:output=0<0,1>-Output


@init
// Enums
timebase_real = 0;
timebase_tempo = 1;

// Midi events
midi_event_note_on = $x90;
midi_event_note_off = $x80;

// Members
lfo_time = 0;

function handle_midi_events()
(
  while(midirecv(offset, msg1, msg2, msg3))
  (
    current_note = msg2;
    
    msg1 == midi_event_note_on ?
    (
      lfo_time = 1;
    );
  );
);

function get_lfo_length()
(
  timebase == timebase_real ? length_ms :
  timebase == timebase_tempo ? 60000 / tempo * length_beats;
);

function get_lfo_step_size()
(
  srate > 0 ? 1 / (srate * get_lfo_length() / 1000) : 0;
);


@slider
slider_show(length_ms, timebase == timebase_real);
slider_show(length_beats, timebase == timebase_tempo);


@block
handle_midi_events();


@sample
lfo_time > 0 ? lfo_time -= get_lfo_step_size();
lfo_time < 0 ? lfo_time = 0;
output = 1 - lfo_time;


@gfx
gfx_clear = 0;
gfx_r = 1; gfx_g = 1; gfx_b = 0;
gfx_rect(0, 0, gfx_w * output, gfx_h);
